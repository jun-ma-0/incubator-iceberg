/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

subprojects {
  apply plugin: 'maven' // make pom files for deployment
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    group 'build'
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    group 'build'
  }

  task testJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
  }

  artifacts {
    archives sourceJar
    archives javadocJar
    archives testJar
    testArtifacts testJar
  }

  // add LICENSE and NOTICE
  [jar, sourceJar, javadocJar, testJar].each { task ->
    task.from(rootDir) {
      include 'LICENSE'
      include 'NOTICE'
    }
  }

  publishing {
    publications {
      apache(MavenPublication) {
        from components.java

        artifact sourceJar
        artifact javadocJar
        artifact testJar

        versionMapping {
          allVariants {
            fromResolutionResult()
          }
        }

        groupId = 'org.apache.iceberg'
        pom {
          name = 'Apache Iceberg (incubating)'
          description = 'A table format for huge analytic datasets'
          url = 'https://iceberg.apache.org'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          mailingLists {
            mailingList {
              name = 'Dev Mailing List'
              post = 'dev@iceberg.apache.org'
              subscribe = 'dev-subscribe@iceberg.apache.org'
              unsubscribe = 'dev-unsubscribe@iceberg.apache.org'
            }
          }
          issueManagement {
            system = 'GitHub'
            url = 'https://github.com/apache/incubator-iceberg/issues'
          }
        }
      }
    }

    // Adobe repositories
    repositories {
      maven {
        name 'Experience Platform Repositories'
        def releasesRepoUrl  = "https://artifactory.corp.adobe.com/artifactory/maven-experienceplatform-release/"
        def snapshotsRepoUrl = "https://artifactory.corp.adobe.com/artifactory/maven-experienceplatform-snapshot/"
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = "${artifactory_user_p}" == "" ? System.getenv("ARTIFACTORY_USER") : "${artifactory_user_p}"
          password = "${artifactory_key_p}" == "" ? System.getenv("ARTIFACTORY_API_TOKEN") : "${artifactory_key_p}"
        }
      }
    }
  }

  if (project.hasProperty('release')) {
    signing {
      useGpgCmd()
      sign publishing.publications.apache
    }
  }
}
